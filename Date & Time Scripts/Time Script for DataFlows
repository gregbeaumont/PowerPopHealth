let
  MinuteCount = 1440,
  Source = List.Times(#time(0, 0, 0),MinuteCount, #duration(0,0,1,0)),
  TableFromList = Table.FromList(Source, Splitter.SplitByNothing()),
  ChangedType = Table.TransformColumnTypes(TableFromList, {{"Column1", type time}}),
  RenamedColumns = Table.RenameColumns(ChangedType, {{"Column1", "Time"}}),
  InsertHour = Table.AddColumn(RenamedColumns, "Hour", each Time.StartOfHour([Time]), type time),
  InsertMinute = Table.AddColumn(InsertHour, "Minute", each Time.Minute([Time]), type number),
  ChangedTypeHour = Table.TransformColumnTypes(InsertMinute, {{"Hour", type time}}),
  InsertQuarterHour = Table.AddColumn(ChangedTypeHour, "Quarter Hour", each if [Minute]<15 then [Hour] else if [Minute] < 30 then Value.Add([Hour],#duration(0,0,15, 0)) else if [Minute] < 45 then Value.Add([Hour],#duration(0,0,30, 0)) else Value.Add([Hour],#duration(0,0,45, 0)), type time),
  ChangedTypeQtrHr = Table.TransformColumnTypes(InsertQuarterHour, {{"Quarter Hour", type time}}),
  ReorderedColumns = Table.ReorderColumns(ChangedTypeQtrHr, {"Time", "Hour", "Quarter Hour", "Minute"}),
  InsertHourNumber = Table.AddColumn(ReorderedColumns, "Hour Number", each Time.Hour([Time]), type number),
  NextHour = Table.AddColumn(InsertHourNumber, "Next Hour", each Value.Add([Hour],#duration(0,1,0, 0)), type time),
  NextQuarterHour = Table.AddColumn(NextHour, "Next Quarter Hour", each Value.Add([Quarter Hour],#duration(0,0,15, 0)), type time),
  InsertPeriod = Table.AddColumn(NextQuarterHour, "Period of Day", each if [Hour Number] >= 0 and [Hour Number] < 4 then "After Midnight" else 
 if [Hour Number] >= 4 and [Hour Number] < 8 then "Early Morning" else
 if [Hour Number] >= 8 and [Hour Number] < 12 then "Late Morning" else
 if [Hour Number] >= 12 and [Hour Number] < 16 then "Afternoon" else
 if [Hour Number] >= 16 and [Hour Number] < 20 then "Evening" else "Late Night", type text),
  InsertPeriodSort = Table.AddColumn(InsertPeriod, "PeriodSort", each if [Hour Number] >= 0 and [Hour Number] < 4 then 0 else 
 if [Hour Number] >= 4 and [Hour Number] < 8 then 1 else
 if [Hour Number] >= 8 and [Hour Number] < 12 then 2 else
 if [Hour Number] >= 12 and [Hour Number] < 16 then 3 else
 if [Hour Number] >= 16 and [Hour Number] < 20 then 4 else 5, type number),
  InsertTimeKey = Table.AddColumn(InsertPeriodSort, "TimeKey", each Time.ToText([Time], "HHmm"), type text),
  InsertAMorPM = Table.AddColumn(InsertTimeKey, "AM or PM", each if [Hour Number] >= 12 then "PM" else "AM", type text),
  #"Changed column type" = Table.TransformColumnTypes(InsertAMorPM, {{"Minute", Int64.Type}, {"Hour Number", Int64.Type}, {"PeriodSort", Int64.Type}})
in
  #"Changed column type"
